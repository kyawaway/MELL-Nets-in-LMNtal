{ 
system_ruleset.
// basic
//// tensor-par
cut_elimination_tensorpar@@
tensor(X1,Y1,XY1),par(X2,Y2,XY2),cut{cut_(XY1),cut_(XY2)} 
  :- cut{cut_(X1),cut_(X2)},cut{cut_(Y1),cut_(Y2)}.

//// ax-cut
cut_elimination_axcut@@
cut{cut_(X),cut_(Y)},ax{ax_(Y),ax_(Z)} 
  :- X=Z.

// box
//// dereliction
box_cut_elimination_dereliction@@
{'!'(X1,X2),$g[X1|*X],@r},cut{cut_(X2),cut_(X3)},'?d'(X4,X3) 
  :- cut{cut_(X1),cut_(X4)}, $g[X1|*X],@r.

//// nested
box_cut_elimination_nested@@
{'!'(X1,X2),$g1[X1|*X]},cut{cut_(X2),cut_(X3)},{$g2[X3|*X]}
  :- {{'!'(X1,X2),$g1[X1|*X]},cut{cut_(X2),cut_(X3)},$g2[X3|*X]}.

//// weakening
box_cut_elimination_weakening@@
{'!'(X1,X2),$g[X1|*X]},cut{cut_(X2),cut_(X3)},'?w'(X3) 
  :- mell.delete(X1,W),{$g[X1|*X]},{'?w'(W)}.

//// contration
box_cut_elimination_contraction@@
{'!'(X1,X2),$g[X1|*X]},cut{cut_(X2),cut_(X3)},'?c'({'?c_'(C1),'?c_'(C2)},X3)
  :- mell.copy(X2,A1,A2,A3,B1,B2,C1,C2),{'!'(X1,X2),$g[X1|*X]},
      {'?c'({'?c_'(A1),'?c_'(A2)},A3)},{cut{cut_(B1),cut_(B2)}}.

}.

// test (lambda)

/////// 1
ax{ax_(A1),ax_(A2)},'?d'(A1,A3),'?w'(A4).
{ax{ax_(B1),ax_(B2)},'?d'(B1,B3),'?w'(B4),'!'(B2,B5)}.
'?c'({'?c_'(A3),'?c_'(B4)},C1).
'?c'({'?c_'(A4),'?c_'(B3)},C2).
tensor(B5,T1,D2),ax{ax_(T1),ax_(T2)}.
cut{cut_(A2),cut_(D2)}.

par(C2,T2,P1).
par(C1,P1,F).

{
ax{ax_(E1),ax_(E2)},'?d'(E1,E3),par(E3,E2,E4),'!'(E4,E5).
}.
tensor(E5,T3,D4),ax{ax_(T3),ax_(T4)}.
cut{cut_(F),cut_(D4)}.
f(T4).


//{ax{ax_(A1),ax_(A2)},par(A1,A2,A3),'!'(A3,A4)}.
//{ax{ax_(B1),ax_(B2)},par(B1,B2,B3),'!'(B3,B4)}.
//{
//  ax{ax_(C1),ax_(C2)},ax{ax_(C3),ax_(C4)},ax{ax_(C5),ax_(C6)},
//  tensor(C4,C5,C7),
//  '?d'(C7,C8),
//  cut{cut_(C2),cut_(C8)},
//  '?c'({'?c_'(C1),'?c_'(C3)},C9),
//  par(C9,C6,C10),
//  '!'(C10,C11)
//}.
//cut{cut_(C11),cut_(TTD1)}.
//
//tensor(A4,TA1,TA2),ax{ax_(TA1),ax_(TA3)}.
//'?d'(TA2,TD1).
//cut{cut_(TD1),cut_(B4)}.
//
//tensor(TA3,TTA1,TTA2),ax{ax_(TTA1),ax_(TTA3)}.
//'?d'(TTA2,TTD1).
//
//f(TTA3).

